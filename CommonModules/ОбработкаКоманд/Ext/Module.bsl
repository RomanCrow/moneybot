
#Область ПрограммныйИнтерфейс

Процедура ОбработкаКоманды(ПараметрыКоманды) Экспорт
	
	Команда = ПараметрыКоманды.Команда;
	
	ПараметрыКоманды.Вставить("ВидСредств", ПолучитьВидСредств(ПараметрыКоманды.ТекстСообщения));
	ПараметрыКоманды.Вставить("ТипПланированияОперации", ПолучитьТипПланированияОперации(ПараметрыКоманды.ТекстСообщения));
	ПараметрыКоманды.Вставить("СуммыСообщения", СлужебныеПроцедурыКлиентСервер.ПолучитьЧислаИзСтроки(ПараметрыКоманды.ТекстСообщения));
	ПараметрыКоманды.Вставить("Статья", ПолучитьСтатьюПриходаРасхода(ПараметрыКоманды.ТекстСообщения));
	
	Если Команда = Перечисления.Операции.ПоступлениеСредств Или Команда = Перечисления.Операции.РасходСредств Тогда
		ОформитьОперацииПоступленияСписания(ПараметрыКоманды);
	ИначеЕсли Команда = Перечисления.Операции.СведениеКОстатку Тогда
		СвестиКОстатку(ПараметрыКоманды);
	ИначеЕсли ПараметрыКоманды.Команда = Перечисления.Операции.ПеремещениеСредств Тогда 	
		ОформитьПеремещениеСредств(ПараметрыКоманды);
	ИначеЕсли ПараметрыКоманды.Команда = Перечисления.Операции.КраткийОтчет Тогда			
		ПараметрыКоманды.ТекстОтвета = ПараметрыКоманды.ТекстОтвета + ТекстОтветаУспешнаяОперация(ПараметрыКоманды);
	ИначеЕсли ПараметрыКоманды.Команда = Перечисления.Операции.ПолныйОтчет Тогда
		СформироватьДетальныйОтчет(ПараметрыКоманды);
	ИначеЕсли Команда = Перечисления.Операции.ПервоеСообщение Тогда
		ПараметрыКоманды.ТекстОтвета = ТекстСообщенияПриветствие(ПараметрыКоманды.Отправитель);
	Иначе
		ПараметрыКоманды.ТекстОтвета = ПараметрыКоманды.ТекстОтвета + ТекстОтветаНеизвестнаяОперация();
	КонецЕсли; 
	
	
	Если ПараметрыКоманды.Команда = Перечисления.Операции.ОперацииСФизЛицами Тогда
		
		//ВыполнитьОперацииСФизЛицами(ПараметрыКоманды);
		
	ИначеЕсли ПараметрыКоманды.Команда = Перечисления.Операции.ОбработкаНоменклатуры Тогда
		
		//Если СтрНайти(ПараметрыКоманды.ТекстСообщения, "добавить") <> 0 Или СтрНайти(ПараметрыКоманды.ТекстСообщения, "новы") <> 0  Тогда
		//	ДобавитьТовары(ПараметрыКоманды);
		//КонецЕсли;
					
	ИначеЕсли ПараметрыКоманды.Команда = Перечисления.Операции.ОбработкаЧека Тогда 	
		
		//ПроверкаЧеков.ПроверитьЧек(ПараметрыКоманды);
 						
	ИначеЕсли ПараметрыКоманды.Команда = Перечисления.Операции.ОбработкаДолга Тогда
		
		//ВыполнитьОперацииПоДолгам(ФорматированноеСообщение, Команда, ТекстСообщения, СтруктураСообщения.message_id, БезНал, ПараметрыКоманды.ТекстОтвета);  
		
	ИначеЕсли ПараметрыКоманды.Команда = Перечисления.Операции.РаспределениеЗатрат Тогда
		
		//РаспределитьЗатраты(ФорматированноеСообщение, ПараметрыКоманды.ТекстОтвета);
		
	Иначе
		
		//Если СтруктураСообщения.Свойство("photo") Тогда
		//	ПараметрыКоманды.ТекстОтвета = ПараметрыКоманды.ТекстОтвета + "Не удалось распознать код на изображении. Повторите операцию.";
		//Иначе
		//	ПараметрыКоманды.ТекстОтвета = ПараметрыКоманды.ТекстОтвета + ТекстОтветаНеизвестнаяОперация();
		//КонецЕсли;
														
	КонецЕсли;
	
КонецПроцедуры

Функция ОстаткиДенежныхСредств(Период = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыСредств.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ВидыСредств
	|ИЗ
	|	Перечисление.ВидыСредств КАК ВидыСредств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВидыСредств.Ссылка КАК ВидСредств,
	|	ЕСТЬNULL(СредстваОстатки.СуммаОстаток, 0) КАК Сумма
	|ИЗ
	|	ВТ_ВидыСредств КАК ВТ_ВидыСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Средства.Остатки(
	|			//Период//
	|			, ) КАК СредстваОстатки
	|		ПО ВТ_ВидыСредств.Ссылка = СредстваОстатки.ВидСредств"; 
	
	Если Период <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Период//", "&Период");
	КонецЕсли; 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда	
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Вставить(Выборка.ВидСредств, Выборка.Сумма);
		КонецЦикла; 	
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

Функция ОстатокДолга(Выдавший) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДолгиОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.Долги.Остатки(
	|			,
	|			Выдавший = &Выдавший
	|				И НЕ Долг.Закрыт) КАК ДолгиОстатки";
	Запрос.УстановитьПараметр("Выдавший", Выдавший);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.СуммаОстаток;
	КонецЕсли;
	 
	
КонецФункции
 
Функция ПолучитьТекстДетальногоАнализаЗаПериод(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Ссылка,
	|	Пользователи.ИДЧата КАК ИДЧата
	|ПОМЕСТИТЬ Пользователи
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи";
	Если Не Параметры.ВсеПользователи И Параметры.Пользователь <> Неопределено Тогда
		Запрос.УстановитьПараметр("Пользователь", Параметры.Пользователь);
		Запрос.Текст = Запрос.Текст + "
		|ГДЕ Пользователи.Ссылка = &Пользователь";
	КонецЕсли;
	Запрос.Текст = Запрос.Текст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	СредстваОбороты.Статья КАК Статья,
	|	ЕСТЬNULL(СредстваОбороты.СуммаПриход, 0) КАК СуммаПриход,
	|	ЕСТЬNULL(СредстваОбороты.СуммаРасход, 0) КАК СуммаРасход
	|ИЗ
	|	Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Средства.Обороты(&НачалоДня, &КонецПериода, , ) КАК СредстваОбороты
	|		ПО Пользователи.Ссылка = СредстваОбороты.Пользователь
	|ИТОГИ 
	|	Сумма(СуммаПриход),
	|   Сумма(СуммаРасход)
	|ПО
	|	Пользователь";
	Запрос.УстановитьПараметр("НачалоДня", Параметры.Начало);
	Запрос.УстановитьПараметр("КонецПериода", Параметры.Конец);
	
	Если Параметры.Конец - Параметры.Начало < 86400 Тогда
		ТекстПериода = Формат(Параметры.Конец, "ДЛФ=D");
	Иначе
		ТекстПериода = Строка(Формат(Параметры.Начало, "ДЛФ=D")) + " - " + Строка(Формат(Параметры.Конец, "ДЛФ=D")) 
	КонецЕсли; 
	
	ТекстСообщения = "Детализация за " + ТекстПериода + ":
	                |************************************************";
	
	ВыборкаПользователь = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	
	Пока ВыборкаПользователь.Следующий() Цикл
		ТекстСообщения = ТекстСообщения + "
		|<<" + ВыборкаПользователь.Пользователь + ">> :
		|
		|  1)Доходы:";
		
		ВыборкаДетальные = ВыборкаПользователь.Выбрать();
		Пока ВыборкаДетальные.Следующий() Цикл
			Если ВыборкаДетальные.СуммаПриход <> 0 Тогда
				ТекстСообщения = ТекстСообщения + "
				|   * " + ВыборкаДетальные.Статья + " - " + ВыборкаДетальные.СуммаПриход + " руб.";
			КонецЕсли; 	
		КонецЦикла; 
		
		ТекстСообщения = ТекстСообщения + "
		|  ______________________________________________
		|  ИТОГО: " + ВыборкаПользователь.СуммаПриход + " руб.
		|
		|  2)Расходы:";
		
		ВыборкаДетальные.Сбросить();
		Пока ВыборкаДетальные.Следующий() Цикл
			Если ВыборкаДетальные.СуммаРасход <> 0 Тогда
				ТекстСообщения = ТекстСообщения + "
				|   * " + ВыборкаДетальные.Статья + " - " + ВыборкаДетальные.СуммаРасход;
			КонецЕсли; 	
		КонецЦикла;
		
		ТекстСообщения = ТекстСообщения + "
		|  ______________________________________________
		|  ИТОГО: " + ВыборкаПользователь.СуммаРасход + " руб.
		|************************************************
        |";		
	КонецЦикла; 
	
	Остатки = ОстаткиДенежныхСредств();
	
	ТекстСообщения = ТекстСообщения + "
		| Остаток на конец периода: 
		|  * Наличные - " + Остатки[Перечисления.ВидыСредств.Наличные] + " руб.
		|  * Безналичные - " + Остатки[Перечисления.ВидыСредств.Безналичные] + " руб.";
	
	Возврат ТекстСообщения;
	
КонецФункции

Функция СуммаПотраченныхДенежныхСредствЗаПериод(Пользователь, Начало, Конец)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СредстваОбороты.СуммаРасход КАК Расход
	|ИЗ
	|	РегистрНакопления.Средства.Обороты(
	|			&Начало,
	|			&Конец,
	|			Авто,
	|			ТипОперации <> ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Перемещение)
	|				И ТипОперации <> ЗНАЧЕНИЕ(Справочник.СтатьиДоходаРасхода.СведениеКОстатку)
	|				И Пользователь = &Пользователь) КАК СредстваОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	СредстваОбороты.СуммаРасход";
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Начало", Начало);
	Запрос.УстановитьПараметр("Конец", Конец);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Расход;
	Иначе
		Возврат 0;
	КонецЕсли; 		 
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВидСредств(ТекстСообщения)
	
	Вероятности = НейронныеСети.ПолучитьВероятности(Справочники.ЛинейныеНейронныеСети.ОпределениеТипаДенежныхСредств, ТекстСообщения);
	
	Если Вероятности = Неопределено Тогда
		Возврат Перечисления.ВидыСредств.Наличные;
	Иначе
		Возврат Вероятности[0].Нейрон;
	КонецЕсли; 
	
КонецФункции

Функция ПолучитьТипПланированияОперации(ТекстСообщения)
	
	Вероятности = НейронныеСети.ПолучитьВероятности(Справочники.ЛинейныеНейронныеСети.ОпределениеТипаПланированияОперации, ТекстСообщения);
	
	Если Вероятности = Неопределено Тогда
		Возврат Перечисления.ТипПланированияОперации.ФактическаяОперация;
	Иначе
		Возврат Вероятности[0].Нейрон;
	КонецЕсли;
	
КонецФункции
 
Процедура ВыполнитьОперацииСФизЛицами(Параметры)
	
	ТекстСообщения = Параметры.ТекстСообщения;
	ТекстОтвета = Параметры.ТекстОтвета;
	
	Если СтрНайти(ТекстСообщения, "создать") <> 0 Тогда
		
		Данные = СтрЗаменить(ТекстСообщения, "создать физлицо ", "");
		Данные = СтрРазделить(Данные,",", Ложь);
		СпрФЛ = Справочники.ФизическиеЛица.СоздатьЭлемент();
		СпрФЛ.Наименование = ТРег(Данные[0]);
		СпрФЛ.НомерКарты = ?(Данные.Количество() > 1, Данные[1], "");
		СпрФЛ.Записать();
		ТекстОтвета = ТекстОтвета + "Создано физлицо: " + СпрФЛ.Наименование + ?(ЗначениеЗаполнено(СпрФЛ.НомерКарты),", номер карты: " + СпрФЛ.НомерКарты,"");
	ИначеЕсли СтрНайти(ТекстСообщения, "изменить") <> 0 Тогда 	
		
		ФизЛицо = НайтиФизЛицо(ТекстСообщения);
		
		Если ФизЛицо = Неопределено Тогда
			ТекстОтвета = ТекстОтвета + "Ошибка команды! В базе отсутствует физическое лицо.";
		Иначе
			НомерКарты = СтрЗаменить(Строка(СлужебныеПроцедурыКлиентСервер.ПолучитьЧислаИзСтроки(ТекстСообщения)[0]),Символы.НПП,"");
			Если НомерКарты = 0 Тогда
				ТекстОтвета = ТекстОтвета + "Введите номер карты физлица!";
			Иначе
				СпрФЛ = ФизЛицо.ПолучитьОбъект();
				СпрФЛ.НомерКарты = НомерКарты;
				СпрФЛ.Записать();
				ТекстОтвета = ТекстОтвета + "Номер карты физлица " + ФизЛицо + " изменен!";
			КонецЕсли; 
		КонецЕсли; 
		
	ИначеЕсли СтрНайти(ТекстСообщения, "информация") <> 0 Тогда
		ФизЛицо = НайтиФизЛицо(ТекстСообщения);
		
		Если ФизЛицо = Неопределено Тогда
			ТекстОтвета = ТекстОтвета + "Ошибка команды! В базе отсутствует физическое лицо.";
		Иначе
			ТекстОтвета = ТекстОтвета + "Информация о физлице: " + ФизЛицо + ":
										|________________________________________
										|   *Номер карты: " + ФизЛицо.НомерКарты; 
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры
 
Процедура ДобавитьТовары(Параметры)
	
	ТекстСообщения = Параметры.ТекстСообщения;
	ТекстОтвета = Параметры.ТекстОтвета;
	
	МассивДанных = СтрРазделить(ТекстСообщения, ":");
	
	Статья = ПолучитьСтатьюПриходаРасхода(МассивДанных[0]);
	
	МассивТоваров = СтрРазделить(МассивДанных[1],",");
	
	НазванияТоваровСтрока = "";
	
	Для Каждого Товар Из МассивТоваров Цикл
		
		ДанныеТовара = СтрРазделить(Товар, "-", Ложь);
		
		ИмяТовара = СокрЛП(ДанныеТовара[0]);
		Если ДанныеТовара.Количество() <> 1 Тогда
			МассивКлючей = СтрРазделить(ДанныеТовара[1],";",Ложь);
		КонецЕсли;
		
		ТакойжеТовар = Справочники.Товары.НайтиПоНаименованию(ИмяТовара, Ложь);
		Если ТакойжеТовар.Пустая() Тогда
			
			НовСпр = Справочники.Товары.СоздатьЭлемент();
			НовСпр.Наименование = ИмяТовара;
			НовСпр.Статья = Статья;
			
			НовСтр = НовСпр.Ключи.Добавить();
			НовСтр.Ключ = ИмяТовара;
			
			Если МассивКлючей <> Неопределено Тогда
				
				Для Каждого Ключ Из МассивКлючей Цикл
					
					НовСтр = НовСпр.Ключи.Добавить();
					НовСтр.Ключ = СокрЛП(Ключ);
					
				КонецЦикла;
				
			КонецЕсли;
			
			НовСпр.Записать();
			
		КонецЕсли;
		НазванияТоваровСтрока = НазванияТоваровСтрока + Символы.ПС + НовСпр.Наименование;
	КонецЦикла;
	
	ТекстОтвета = ТекстОтвета + "Добавлены товары категории " + Статья + ":" + НазванияТоваровСтрока;
	
КонецПроцедуры
 
Процедура ВыполнитьОперацииПоДолгам(ТекстСообщения, Команда, Комментарий, ИДСообщения, БезНал, ТекстОтвета)
	
	Выдавший = НайтиФизЛицо(ТекстСообщения);
	
	НашлиФизЛицо = ?(Выдавший = Неопределено, Ложь, Истина);
	
	Если СтрНайти(ТекстСообщения, "новый") <> 0 И НашлиФизЛицо Тогда
		
		СпрДолг = Справочники.Долг.СоздатьЭлемент();
		СпрДолг.Выдавший = Выдавший;
		СпрДолг.Сумма = СлужебныеПроцедурыКлиентСервер.ПолучитьЧислаИзСтроки(ТекстСообщения)[0];
		СпрДолг.Наименование = "Взят долг у " + Выдавший;
		СпрДолг.Безнал = БезНал;
		СпрДолг.НеСоздаватьПоступление = ?(СтрНайти(ТекстСообщения, "остат") <> 0, Истина, Ложь);
		СпрДолг.Комментарий = Комментарий;
		СпрДолг.ИДСообщения = ИДСообщения;
		СпрДолг.Записать();
		ТекстОтвета = ТекстОтвета + "Создан новый долг. Выдавший: " + Выдавший + ", сумма: " + СпрДолг.Сумма + " руб."; 
			
	ИначеЕсли СтрНайти(ТекстСообщения, "уменьш") <> 0 И НашлиФизЛицо Тогда
		
		Сумма = -СлужебныеПроцедурыКлиентСервер.ПолучитьЧислаИзСтроки(ТекстСообщения)[0];
		
		ТекДолг = НайтиДолг(Выдавший);
		Если ТекДолг = Неопределено Тогда
			
			ТекстОтвета = ТекстОтвета + " У физлица " + Выдавший + " нет неоплаченных долгов.";
			
		Иначе
		
			Успешно = ДвижениеДолга(ТекДолг, Сумма, Комментарий, ИДСообщения, БезНал);
			
			Если Успешно Тогда
			 
				ОстатокДолга = ОстатокДолга(Выдавший);
				
				Если ОстатокДолга = 0 Тогда
					
					ТекстОтвета = ТекстОтвета + "Долг " + Выдавший + " оплачен! Поздравляю!!!";
					
				Иначе
					
					ТекстОтвета = ТекстОтвета + "Долг " + Выдавший + " уменьшен на " + -Сумма + " руб. Остаток долга: " + ОстатокДолга + " руб.";
				
				КонецЕсли; 
				
				//ТекстОтвета = ТекстОтвета + Символы.ПС + ТекстОтветаУспешнаяОперация(,,,Ложь);
				
			Иначе
				
				//ТекстОтвета = ТекстОтвета + "Недостаточно средств для списания!" + Символы.ПС + ТекстОтветаУспешнаяОперация(,,,Ложь);
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли СтрНайти(ТекстСообщения, "увелич") <> 0 И НашлиФизЛицо Тогда
		
		Сумма = СлужебныеПроцедурыКлиентСервер.ПолучитьЧислаИзСтроки(ТекстСообщения)[0];
		
		ТекДолг = НайтиДолг(Выдавший);
		Если ТекДолг = Неопределено Тогда
			
			ТекстОтвета = ТекстОтвета + " У физлица " + Выдавший + " нет неоплаченных долгов. Создайте новый долг.";
			
		Иначе
		
			Успешно = ДвижениеДолга(ТекДолг, Сумма, Комментарий, ИДСообщения, БезНал);
			 
			//ТекстОтвета = ТекстОтвета + "Долг физлицу: " + Выдавший + " увеличен на " + Сумма + " руб. Остаток долга: " + ОстатокДолга(Выдавший) + Символы.ПС + ТекстОтветаУспешнаяОперация(,,,Ложь);
		
		КонецЕсли;
		
	ИначеЕсли Команда = "ПоказатьОстаток" Тогда
		
		ТекстОтвета = ТекстОтвета + ТекстОтветаОстаткиДолгов(?(НашлиФизЛицо,Выдавший,Неопределено));
		
	ИначеЕсли Команда = "ДетальныйАнализ" И НашлиФизЛицо Тогда 
		
		ТекстОтвета =  ТекстОтвета + ТекстДетальныйАнализДолга(Выдавший);
		
	ИначеЕсли Не НашлиФизЛицо Тогда
		
		ТекстОтвета = ТекстОтвета + "В базе отсутствует физическое лицо. Создайте его, написав: ""Новое физлицо [Имя], [Номер карты]""";		
		
	Иначе	
		ТекстОтвета = ТекстОтвета + "Извините, мне не понятно, что вы хотите мне сообщить...";
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

Функция ПолучитьСтатьюПриходаРасхода(ТекстСообщения)
	
	Возврат Справочники.СтатьиДоходаРасхода.Прочее;
	
КонецФункции 

Функция ПолучитьПараметрыАнализа(ПараметрыОперации)

	Результат = Новый Структура("ВсеПользователи,Начало,Конец,Пользователь");
	Результат.Пользователь = ПараметрыОперации.Отправитель;
	
	ТекстСообщения = НРег(ПараметрыОперации.ТекстСообщения);
	
	Результат.ВсеПользователи = СтрНайти(ТекстСообщения,"все") <> 0;
	
	Следующий = СтрНайти(ТекстСообщения, "следующ") <> 0 Или СтрНайти(ТекстСообщения, "завтра") <> 0;
	Предыдущий = СтрНайти(ТекстСообщения, "предыдущ") <> 0 Или СтрНайти(ТекстСообщения, "вчера") <> 0;
	
	ТекДата = ТекущаяДатаСеанса();		
	
	Если СтрНайти(ТекстСообщения, "месяц") Тогда
		Если Следующий Тогда
			ТекДата = КонецМесяца(ТекДата) + 1;	
		ИначеЕсли Предыдущий Тогда 	
			ТекДата = НачалоМесяца(ТекДата) - 1;
		КонецЕсли;  
		Начало = НачалоМесяца(ТекДата);
		Конец = КонецМесяца(ТекДата);
	ИначеЕсли СтрНайти(ТекстСообщения, "квартал") Тогда
		Если Следующий Тогда
			ТекДата = КонецКвартала(ТекДата) + 1;	
		ИначеЕсли Предыдущий Тогда 	
			ТекДата = НачалоКвартала(ТекДата) - 1;
		КонецЕсли;
		Начало = НачалоКвартала(ТекДата);
		Конец = КонецКвартала(ТекДата);
	ИначеЕсли СтрНайти(ТекстСообщения, "год") Тогда
		Если Следующий Тогда
			ТекДата = КонецГода(ТекДата) + 1;	
		ИначеЕсли Предыдущий Тогда 	
			ТекДата = НачалоГода(ТекДата) - 1;
		КонецЕсли;
		Начало = НачалоГода(ТекДата);
		Конец = КонецГода(ТекДата);
	ИначеЕсли СтрНайти(ТекстСообщения, "неделю") Тогда
		Если Следующий Тогда
			ТекДата = КонецНедели(ТекДата) + 1;	
		ИначеЕсли Предыдущий Тогда 	
			ТекДата = НачалоНедели(ТекДата) - 1;
		КонецЕсли;
		Начало = НачалоНедели(ТекДата);
		Конец = КонецНедели(ТекДата);
	Иначе
		
		Период = ОбработатьПериодПоМесяцамОтДаты(ТекстСообщения, ТекДата, Следующий, Предыдущий);
		
		Если Период = Неопределено Тогда
					 
			Если Следующий Тогда
				ТекДата = КонецДня(ТекДата) + 1;	
			ИначеЕсли Предыдущий Тогда 	
				ТекДата = НачалоДня(ТекДата) - 1;
			КонецЕсли;
			Начало = НачалоДня(ТекДата);
			Конец = КонецДня(ТекДата);
			
		Иначе
			
			Начало = Период.Начало;
			Конец = Период.Конец;
			
		КонецЕсли;
	КонецЕсли; 
	
	Результат.Начало = Начало;
	Результат.Конец = Конец;
	
	Возврат Результат;
	
КонецФункции
 

Процедура РаспределитьЗатраты(ТекстСообщения, ТекстОтвета) Экспорт
	
	//Сумма ежедневных затрат
	СуммаДень = Константы.МаксимальнаяСуммаЕжедневныхЗатрат.Получить();
	Если СуммаДень = 0 Тогда
		ТекстОтвета = ТекстОтвета + "Кажется, вы не установили максимальную сумму ежедневных затрат на незапланированные расходы...";
		Возврат;
	КонецЕсли; 
	
	//Можно меньше, мо не менее 75% от суммы ежедневных затрат
	МинимумЗатрат = СуммаДень / 100 * 75;
	
	ТекДата = ТекущаяДатаСеанса();
	
	//период распределения: до конца следующего месяца
	Если СтрНайти(ТекстСообщения,"следующ") <> 0 Тогда
		
		Начало = КонецМесяца(ТекДата) + 1;
	    Конец = КонецМесяца(Начало);
		
	Иначе
		
		Если СтрНайти(ТекстСообщения,"включ") <> 0 Или СтрНайти(ТекстСообщения,"сегодн") <> 0 Тогда
			Начало = НачалоДня(ТекущаяДатаСеанса());
		Иначе
			Начало = КонецДня(ТекущаяДатаСеанса()) + 1;
		КонецЕсли; 
		
		Конец = КонецМесяца(Начало);
		
	КонецЕсли; 
		
	//Все плановые операции за период
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ЗапланированныеОперации.Период, ДЕНЬ) КАК Период,
	|	СУММА(ВЫБОР
	|			КОГДА ЗапланированныеОперации.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ПриходРасход.Приход)
	|				ТОГДА ЗапланированныеОперации.Сумма
	|			ИНАЧЕ -ЗапланированныеОперации.Сумма
	|		КОНЕЦ) КАК СуммаПланОпераций
	|ИЗ
	|	РегистрСведений.ЗапланированныеОперации КАК ЗапланированныеОперации
	|ГДЕ
	|	ПланируемыеДоходыРасходы.Период МЕЖДУ &Начало И &Конец
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ПланируемыеДоходыРасходы.Период, ДЕНЬ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	Запрос.УстановитьПараметр("Начало", Начало);
	Запрос.УстановитьПараметр("Конец", Конец);
	
	ТЗЗатрат = Запрос.Выполнить().Выгрузить();
	
	ТЗИтог = Новый ТаблицаЗначений;
	ТЗИтог.Колонки.Добавить("Дата");
	ТЗИтог.Колонки.Добавить("СуммаОпераций");
	ТЗИтог.Колонки.Добавить("Остаток");
	ТЗИтог.Колонки.Добавить("СуммаЗатратВДень");
	
	МассивИндексовПрихода = Новый Массив;
	ТекОстаток = ОстаткиДенежныхСредств();
	Остаток = ТекОстаток;	
	НачальнаяДата = Начало;
	ТекИндексСтроки = 0;
	
	Пока НачальнаяДата < Конец Цикл
		
		НовСтр = ТЗИтог.Добавить();
		НовСтр.Дата = НачальнаяДата;
		
		СтрокаТЗПланОпераций = ТЗЗатрат.Найти(НачальнаяДата,"Период");
		Если СтрокаТЗПланОпераций <> Неопределено Тогда
			
			НовСтр.СуммаОпераций = СтрокаТЗПланОпераций.СуммаПланОпераций;
			
			Если СтрокаТЗПланОпераций.СуммаПланОпераций > 0 Тогда
				МассивИндексовПрихода.Добавить(ТекИндексСтроки);	
			КонецЕсли;
			
		Иначе
			
			НовСтр.СуммаОпераций = 0;
			
		КонецЕсли; 
		
		Остаток = Остаток + НовСтр.СуммаОпераций - СуммаДень;
		НовСтр.СуммаЗатратВДень = ?(НовСтр.СуммаОпераций < 0, -НовСтр.СуммаОпераций, 0);
		НовСтр.Остаток = Остаток;
		
		ТекИндексСтроки = ТекИндексСтроки +1;
		НачальнаяДата = НачальнаяДата + 86400;
		
	КонецЦикла; 
	
	Если МассивИндексовПрихода.Найти(ТекИндексСтроки - 1) = Неопределено Тогда
		МассивИндексовПрихода.Добавить(ТекИндексСтроки - 1);	
	КонецЕсли; 
	
	ПредыдущийИндекс = 0;
	БылоНорм = Ложь;
	СейчасНорм = Ложь;
	Остаток = ТекОстаток;
	Для Каждого Индекс Из МассивИндексовПрихода Цикл
		
		ИтогСуммаЗатрат = 0;
		Для Инд = 0 По Индекс Цикл
			ИтогСуммаЗатрат = ИтогСуммаЗатрат + ТЗИтог[Инд].СуммаЗатратВДень;		
		КонецЦикла; 
		
		СуммаИтогОстатокЗаПериод = Остаток - ИтогСуммаЗатрат - (СуммаДень * (Индекс - ПредыдущийИндекс)); 
		
		Если СуммаИтогОстатокЗаПериод > 0 Тогда
			СейчасНорм = Истина;	
		КонецЕсли; 
		
		Среднее = ИтогСуммаЗатрат / (Индекс + 1);
		
		ПредыдущийИндекс = Индекс;
		
	КонецЦикла; 
	а = 0;
	
КонецПроцедуры

Функция НайтиФизЛицо(ТекстСообщения)
	
	НашлиФизЛицо = Ложь;
	МассивСлов = СтрРазделить(ТекстСообщения, " ",Ложь);
	Запрос= Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФизическиеЛицаСклонения.Ссылка КАК Ссылка,
	|	ФизическиеЛицаСклонения.СклоненноеИмя КАК Наименование
	|ИЗ
	|	Справочник.ФизическиеЛица.Склонения КАК ФизическиеЛицаСклонения";
	Выборка = Запрос.Выполнить().Выбрать();
	Для Каждого Слово Из МассивСлов Цикл
		Пока Выборка.Следующий() Цикл
			Если СтрНайти(НРег(Выборка.Наименование), Слово) <> 0 Тогда
				НашлиФизЛицо = Истина;
				ФизЛицо = Выборка.Ссылка;
				Прервать;
			КонецЕсли;

		КонецЦикла;
		Если НашлиФизЛицо Тогда
			Прервать;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	Возврат ?(НашлиФизЛицо,ФизЛицо,Неопределено);
	
КонецФункции

Функция НайтиДолг(Выдавший)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Долг.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Долг КАК Долг
	|ГДЕ
	|	Долг.Выдавший = &Выдавший
	|	И НЕ Долг.Закрыт
	|	И НЕ Долг.ПометкаУдаления";
	Запрос.УстановитьПараметр("Выдавший", Выдавший);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли; 
	 	
КонецФункции
 
Функция ДвижениеДолга(Долг, Сумма, Комментарий, ИДСообщения, Безнал)
		
	Количество = Пользователи.КоличествоПользователей();
	
	Пользователи = Справочники.Пользователи.Выбрать();

	СуммаНаОдного = Число(Формат(Сумма / Количество, "ЧДЦ=2"));
	ОстатокСуммы = Сумма;
	
	Если Сумма < 0 Тогда
		Остаток = ОстаткиДенежныхСредств(Безнал);
		Если Остаток < -Сумма Тогда
			Возврат Ложь;	
		КонецЕсли; 
	КонецЕсли; 
	
	Пока Пользователи.Следующий() Цикл
		
		Документы.Операция.СоздатьИЗаполнить(Пользователи.Ссылка, ?(СуммаНаОдного < 0,Ложь,Истина), ?(СуммаНаОдного < 0, -СуммаНаОдного, СуммаНаОдного), Справочники.СтатьиДоходаРасхода.ВзятиеВДолг, Безнал, ТекущаяДатаСеанса());	
		
		ОстатокСуммы = ОстатокСуммы - СуммаНаОдного;
		
	КонецЦикла;
	
	Если ОстатокСуммы <> 0 Тогда
		Документы.Операция.СоздатьИЗаполнить(Пользователи.Ссылка, ?(ОстатокСуммы>0,Истина,Ложь), ?(ОстатокСуммы>0,ОстатокСуммы,ОстатокСуммы * -1), Справочники.СтатьиДоходаРасхода.ВзятиеВДолг, Безнал, ТекущаяДатаСеанса());	
	КонецЕсли;	
	
	Док = Документы.ПересчетДолга.СоздатьДокумент();
	Док.Дата = ТекущаяДатаСеанса();
	Док.Долг = Долг; 
	Док.Сумма = Сумма;
	Док.Комментарий = Комментарий;
	Док.ИДСообщения = ИДСообщения;
	Док.Записать(РежимЗаписиДокумента.Проведение);
	
	Если ОстатокДолга(Долг.Выдавший) = 0 Тогда
		СпрДолг = Долг.ПолучитьОбъект();
		СпрДолг.Закрыт = Истина;
		СпрДолг.Записать();
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции

Функция ОбработатьПериодПоМесяцамОтДаты(ТекстСообщения, Дата, Следующий = Ложь, Предыдущий = Ложь)
		
	Если СтрНайти(ТекстСообщения, "январь") Тогда
		Месяц = 1;
	ИначеЕсли СтрНайти(ТекстСообщения, "февраль") Тогда
		Месяц = 2;
	ИначеЕсли СтрНайти(ТекстСообщения, "март") Тогда
		Месяц = 3;
	ИначеЕсли СтрНайти(ТекстСообщения, "апрель") Тогда
		Месяц = 4;
	ИначеЕсли СтрНайти(ТекстСообщения, "май") Тогда
		Месяц = 5;
	ИначеЕсли СтрНайти(ТекстСообщения, "июнь") Тогда
		Месяц = 6;
	ИначеЕсли СтрНайти(ТекстСообщения, "июль") Тогда
		Месяц = 7;
	ИначеЕсли СтрНайти(ТекстСообщения, "август") Тогда
		Месяц = 8;
	ИначеЕсли СтрНайти(ТекстСообщения, "сентябрь") Тогда
		Месяц = 9;
	ИначеЕсли СтрНайти(ТекстСообщения, "октябрь") Тогда
		Месяц = 10;
	ИначеЕсли СтрНайти(ТекстСообщения, "ноябрь") Тогда
		Месяц = 11;
	ИначеЕсли СтрНайти(ТекстСообщения, "декабрь") Тогда
	    Месяц = 12;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Год = Год(Дата);
	
	Если Следующий Тогда
		Год = Год + 1;
	ИначеЕсли Предыдущий Тогда
		Год = Год - 1;
	КонецЕсли; 
	
	Дата = Дата(Год, Месяц, 1);
	
	Возврат Новый Структура("Начало, Конец", Дата, КонецМесяца(Дата));
	
КонецФункции

Функция ТекстСообщенияПриветствие(Отправитель)
	
	ТекстОтвета = "Привет, " + Отправитель + "! 
					|Ты можешь вписывать сюда свои доходы и расходы. Просто напиши сообщение формата: ""приход/расход + сумма"", и я тебя пойму.";
	
	Возврат ТекстОтвета;
		
КонецФункции

Функция ТекстОтветаНеизвестнаяОперация() Экспорт
	
	ТекстОтвета = "Извините, мне не понятно, что вы хотите мне сообщить...";
	
	Возврат ТекстОтвета;
	
КонецФункции

Функция ТекстОтветаОстаткиДолгов(Выдавший = Неопределено)
	ТекстСообщения = "Остатки долгов:" ;
	ВсегоДолгов = 0;
	Если Выдавший <> Неопределено Тогда
		Остаток = ОбработкаКоманд.ОстатокДолга(Выдавший);
		ТекстСообщения = ТекстСообщения + "  * " + Выдавший + " : " + Остаток + " руб.";
		ВсегоДолгов = ВсегоДолгов + Остаток;
	Иначе
		Выборка = Справочники.ФизическиеЛица.Выбрать();
		Пока Выборка.Следующий() Цикл
			Остаток = ОбработкаКоманд.ОстатокДолга(Выборка.Ссылка);
			Если Остаток > 0 Тогда
				ТекстСообщения = ТекстСообщения + Символы.ПС + "  * " + Выборка + " : " + Остаток + " руб.";
			КонецЕсли;
			ВсегоДолгов = ВсегоДолгов + Остаток;
		КонецЦикла;  
	КонецЕсли;
	
	ТекстСообщения = ТекстСообщения + "
					|___________________________________
					| ИТОГО долгов: " + ВсегоДолгов + " руб.";
	
	Возврат ТекстСообщения;
КонецФункции
  
Функция ТекстОтветаПлановыеОперации(ПараметрыАнализа)
	
	Начало = ПараметрыАнализа.Начало;
	Конец = ПараметрыАнализа.Конец;
	
	Текст = "Планируемые операции за период " + Формат(Начало,"ДЛФ=D") + " - " + Формат(Конец,"ДЛФ=D") + "
			|********************************************";
	
	ПлановыеОперацииРезультатЗапроса = ПолучитьПлановыеОперацииЗаПериод(Начало, Конец);
	Выборка = ПлановыеОперацииРезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ИтогоПриход = 0;
	ИтогоРасход = 0;
	Пока Выборка.Следующий() Цикл
		
		Текст = Текст + "
				| < " + Формат(Выборка.Дата,"ДЛФ=D") + " >";
		
		ВыборкаДетЗаписи = Выборка.Выбрать();
		
		Пока ВыборкаДетЗаписи.Следующий() Цикл
			
			ИтогоПриход = ИтогоПриход + ВыборкаДетЗаписи.СуммаПрихода;
			ИтогоРасход = ИтогоРасход + ВыборкаДетЗаписи.СуммаРасхода;
			
			Текст = Текст + "
					| Поступления: " + ВыборкаДетЗаписи.СуммаПрихода + " руб." + ?(ВыборкаДетЗаписи.СуммаПрихода <> 0, " Статья: " + ВыборкаДетЗаписи.СтатьяПрихода,"") + ";
					| Расходы: " + ВыборкаДетЗаписи.СуммаРасхода + " руб." + ?(ВыборкаДетЗаписи.СуммаРасхода <> 0, " Статья: " + ВыборкаДетЗаписи.СтатьяРасхода,"");
			
		КонецЦикла; 
		
		Текст = Текст + "
				|____________________________________";		
		
	КонецЦикла; 
	
	Текст = Текст + " 
			|  ИТОГО:
			| * Поступления: " + ИтогоПриход + " руб.
			| * Расходы: " + ИтогоРасход + " руб.";
	
	Возврат Текст;
	
КонецФункции

Функция ПолучитьПлановыеОперацииЗаПериод(Начало, Конец)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Начало", Начало);
	Запрос.УстановитьПараметр("Конец", Конец);
	Запрос.УстановитьПараметр("ОперацияПоступлениеСредств", Перечисления.Операции.ПоступлениеСредств);
	Запрос.УстановитьПараметр("ОперацияРасходСредств", Перечисления.Операции.РасходСредств);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЗапланированныеОперации.Операция = &ОперацияПоступлениеСредств
	|			ТОГДА ЗапланированныеОперации.Статья
	|	КОНЕЦ КАК СтатьяПрихода,
	|	ВЫБОР
	|		КОГДА ЗапланированныеОперации.Операция = &ОперацияРасходСредств
	|			ТОГДА ЗапланированныеОперации.Статья
	|	КОНЕЦ КАК СтатьяРасхода,
	|	СУММА(ВЫБОР
	|			КОГДА ЗапланированныеОперации.Операция = &ОперацияПоступлениеСредств
	|				ТОГДА ЗапланированныеОперации.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПрихода,
	|	СУММА(ВЫБОР
	|			КОГДА ЗапланированныеОперации.Операция = &ОперацияРасходСредств
	|				ТОГДА ЗапланированныеОперации.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаРасхода,
	|	НАЧАЛОПЕРИОДА(ЗапланированныеОперации.Период, ДЕНЬ) КАК Дата
	|ИЗ
	|	РегистрСведений.ЗапланированныеОперации КАК ЗапланированныеОперации
	|ГДЕ
	|	ЗапланированныеОперации.Период МЕЖДУ &Начало И &Конец
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ЗапланированныеОперации.Период, ДЕНЬ),
	|	ВЫБОР
	|		КОГДА ЗапланированныеОперации.Операция = &ОперацияПоступлениеСредств
	|			ТОГДА ЗапланированныеОперации.Статья
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЗапланированныеОперации.Операция = &ОперацияРасходСредств
	|			ТОГДА ЗапланированныеОперации.Статья
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|ИТОГИ
	|	СУММА(СуммаПрихода),
	|	СУММА(СуммаРасхода)
	|ПО
	|	Дата";
		
	Возврат Запрос.Выполнить();
		
КонецФункции

Функция ТекстДетальныйАнализДолга(Выдавший)
	
	ТекстСообщения = "Анализ долга перед физлицом " + Выдавший + "
					|*****************************************" + Символы.ПС;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДолгиОбороты.Период КАК Период,
	|	СУММА(ДолгиОбороты.СуммаПриход) КАК СуммаПриход,
	|	СУММА(ДолгиОбороты.СуммаРасход) КАК СуммаРасход
	|ИЗ
	|	РегистрНакопления.Долги.Обороты(
	|			,
	|			,
	|			День,
	|			Выдавший = &Выдавший
	|				И НЕ Долг.Закрыт) КАК ДолгиОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДолгиОбороты.Период";
	Запрос.УстановитьПараметр("Выдавший", Выдавший);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИтогПриход = 0;
	ИтогРасход = 0;
	
	Пока Выборка.Следующий() Цикл
		
		ИтогПриход = ИтогПриход + Выборка.СуммаПриход;
		ИтогРасход = ИтогРасход + Выборка.СуммаРасход;
		
		ТекстСообщения = ТекстСообщения + " Дата: " + Формат(Выборка.Период, "ДЛФ=D") + Символы.ПС + 
						?(ЗначениеЗаполнено(Выборка.СуммаПриход), "    *Увеличение: " + Выборка.СуммаПриход + " руб." + Символы.ПС,"") +
						?(ЗначениеЗаполнено(Выборка.СуммаРасход), "    *Уменьшение: " + Выборка.СуммаРасход + " руб." + Символы.ПС,"");	
	КонецЦикла;  
	
	ТекстСообщения = ТекстСообщения + "
	|____________________________________
	| ИТОГО: 
	|   *Получено в долг: " + ИтогПриход + " руб.
	|   *Погашено: " + ИтогРасход + " руб.";
	
	Возврат ТекстСообщения;
	
КонецФункции





Процедура ОформитьОперацииПоступленияСписания(ПараметрыОперации)
	
	Если Не СуммыЗаполнены(ПараметрыОперации.СуммыСообщения) Тогда
		ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОшибкиНетСуммы(ПараметрыОперации.Команда);
		Возврат;
	КонецЕсли; 
	
	Если ПараметрыОперации.ТипПланированияОперации = Перечисления.ТипПланированияОперации.ФактическаяОперация Тогда
		
		СтруктураДокумента = Документы.Операция.ПолучитьСвойстваДокументаНаОснованииСтруктурыОперации(ПараметрыОперации);	
		Успешно = Документы.Операция.СоздатьИЗаполнить(СтруктураДокумента);
		
		Если Успешно Тогда
			ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОтветаУспешнаяОперация(ПараметрыОперации);
		ИначеЕсли ПараметрыОперации.Команда = Перечисления.Операции.РасходСредств Тогда
			ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОтветаНедостаточноСредств(ПараметрыОперации.ВидСредств);
		КонецЕсли; 
				
	Иначе	
		
		ВыполнитьПлановыеОперации(ПараметрыОперации);
		
	КонецЕсли;  
		
КонецПроцедуры
 
Функция СуммыЗаполнены(Суммы)
	
	Если Суммы = Неопределено Или Суммы.Количество() = 0 Или (Суммы.Количество() = 1 И Суммы[0] = 0) Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли; 
	
КонецФункции
 
Функция ТекстОшибкиНетСуммы(Команда)
	
	Если Команда = Перечисления.Операции.ПоступлениеСредств Тогда
		Параметр = "прихода";
	ИначеЕсли Команда = Перечисления.Операции.РасходСредств Тогда
		Параметр = "расхода";
	КонецЕсли; 
	
	ТекстОтвета = "Кажется, вы не написали сумму " + Параметр + ". Перепишите приход правильно...";
	
	Возврат ТекстОтвета;
	
КонецФункции

Функция ТекстОтветаУспешнаяОперация(ПараметрыОперации)
	
	Остатки = ОстаткиДенежныхСредств();
	
	ТекстОтвета = "";
	
	Если ПараметрыОперации.Команда <> Перечисления.Операции.КраткийОтчет Тогда
		ТекстОтвета = "Операция выполнена." + Символы.ПС;
		Если ПараметрыОперации.Команда = Перечисления.Операции.ПоступлениеСредств Тогда
			Параметр = "прихода";
		ИначеЕсли ПараметрыОперации.Команда = Перечисления.Операции.РасходСредств Тогда
			Параметр = "расхода";
		КонецЕсли;
		ТекстОтвета = ТекстОтвета + "
			|Статья " + Параметр + ": " + ПараметрыОперации.Статья;
	КонецЕсли; 
	
	ТекстОтвета = ТекстОтвета + "
	            |********************
				|Остаток средств:
				|Наличные - " + Остатки[Перечисления.ВидыСредств.Наличные] + " руб.
				|Безналичные - " + Остатки[Перечисления.ВидыСредств.Безналичные] + " руб.";
		
	Возврат ТекстОтвета + Символы.ПС;
	                                         
КонецФункции

Функция ТекстОтветаНедостаточноСредств(ВидСредств)
	
	Остаток = ОстаткиДенежныхСредств()[ВидСредств]; 
	
	Если ВидСредств = Перечисления.ВидыСредств.Наличные Тогда
		Параметр = "наличными";
	Иначе
		Параметр = "на карте";
	КонецЕсли; 
	
	ТекстОтвета = "Недостаточно средств. У вас есть только " + Остаток + " руб. " + Параметр + ".";
	
	Возврат ТекстОтвета;
	
КонецФункции

Процедура ВыполнитьПлановыеОперации(ПараметрыОперации)
	
	СуммаСтрокой = СтрЗаменить(Строка(ПараметрыОперации.СуммыСообщения[0]), Символы.НПП, "");
	СтрокаБезСуммы = СтрЗаменить(ПараметрыОперации.ТекстСообщения, СуммаСтрокой, "");
		
	Дата = СлужебныеПроцедурыКлиентСервер.НайтиДатуВСтроке(СтрокаБезСуммы);	
	Запись = РегистрыСведений.ЗапланированныеОперации.СоздатьМенеджерЗаписи();
	Запись.Период = Дата;
	Запись.Операция = ПараметрыОперации.Команда;
	Запись.Пользователь = ПараметрыОперации.Отправитель;
	Запись.Статья = ПараметрыОперации.Статья;
	Запись.Сумма = ПараметрыОперации.СуммыСообщения[0];
	Запись.Записать(Истина);
	
	Если ПараметрыОперации.Команда = Перечисления.Операции.ПоступлениеСредств Тогда
		Параметр = "поступление";
	ИначеЕсли ПараметрыОперации.Команда = Перечисления.Операции.РасходСредств Тогда
		Параметр = "расход";
	КонецЕсли; 
	
	ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + "Операция прошла успешно. Ожидается " + Параметр
				  + " денежных средств " + Формат(Дата, "ДЛФ=DD") + " по статье " + Запись.Статья + ". Сумма: " + Запись.Сумма + " руб.";
	
КонецПроцедуры

Процедура СвестиКОстатку(ПараметрыОперации)
	
	Если Не СуммыЗаполнены(ПараметрыОперации.СуммыСообщения) Тогда
		ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОшибкиНетСуммы(ПараметрыОперации.Команда);
		Возврат;
	КонецЕсли;
	
	Остаток = ОстаткиДенежныхСредств();
	Дельта = ПараметрыОперации.СуммыСообщения[0] - Остаток[ПараметрыОперации.ВидСредств]; 
	
	Если Дельта = 0 Тогда
		ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОтветаУспешнаяОперация(ПараметрыОперации);
		Возврат;
	КонецЕсли; 
			
	КоличествоПользователей = Пользователи.КоличествоПользователей();
	ВыборкаПользователи = Справочники.Пользователи.Выбрать();

	СуммаНаОдного = Число(Формат(Дельта / КоличествоПользователей, "ЧДЦ=2"));
	
	ПараметрыДокумента = Документы.Операция.ПолучитьСвойстваДокументаНаОснованииСтруктурыОперации(ПараметрыОперации);
	ПараметрыДокумента.Сумма = СуммаНаОдного;
	
	Пока ВыборкаПользователи.Следующий() Цикл
		
		ПараметрыДокумента.Пользователь = ВыборкаПользователи.Ссылка;
		
		Документы.Операция.СоздатьИЗаполнить(ПараметрыДокумента);	
		
		Дельта = Дельта - СуммаНаОдного;
		
	КонецЦикла;
	
	Если Дельта <> 0 Тогда
		
		ПараметрыДокумента.Пользователь = ПараметрыОперации.Отправитель;
		ПараметрыДокумента.Сумма = Дельта;
		
		Документы.Операция.СоздатьИЗаполнить(ПараметрыДокумента);
		
	КонецЕсли; 	

	ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОтветаУспешнаяОперация(ПараметрыОперации);
	
КонецПроцедуры

Процедура СформироватьДетальныйОтчет(ПараметрыОперации)
	
	ПараметрыАнализа = ПолучитьПараметрыАнализа(ПараметрыОперации);			
	
	Если ПараметрыОперации.ТипПланированияОперации = Перечисления.ТипПланированияОперации.ФактическаяОперация Тогда	
		ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ПолучитьТекстДетальногоАнализаЗаПериод(ПараметрыАнализа);
	Иначе
		ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОтветаПлановыеОперации(ПараметрыАнализа);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОформитьПеремещениеСредств(ПараметрыОперации)
	
	Сумма = ПараметрыОперации.СуммыСообщения[0];
	
	Остатки = ОстаткиДенежныхСредств();
	
	ФорматированноеСообщение = НРег(ПараметрыОперации.ТекстСообщения);
	Если СтрНайти(ФорматированноеСообщение, " с ") <> 0 Или СтрНайти(ФорматированноеСообщение, " из ") <> 0 Тогда
		ВидСписываемыхСредств = ПараметрыОперации.ВидСредств;
		ВидЗачисляемыхСредств = ?(ПараметрыОперации.ВидСредств = Перечисления.ВидыСредств.Наличные, Перечисления.ВидыСредств.Безналичные, Перечисления.ВидыСредств.Наличные);
	Иначе
		ВидСписываемыхСредств = ?(ПараметрыОперации.ВидСредств = Перечисления.ВидыСредств.Наличные, Перечисления.ВидыСредств.Безналичные, Перечисления.ВидыСредств.Наличные);
		ВидЗачисляемыхСредств = ПараметрыОперации.ВидСредств;
	КонецЕсли; 
	
	Если Остатки[ВидСписываемыхСредств] < Сумма Тогда
		
		ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОтветаНедостаточноСредств(ВидСписываемыхСредств);
		
	Иначе
		
		НачатьТранзакцию();
		ПараметрыЗаполненияДокумента = Документы.Операция.ПолучитьСвойстваДокументаНаОснованииСтруктурыОперации(ПараметрыОперации);
		ПараметрыЗаполненияДокумента.ВидСредств = ВидЗачисляемыхСредств;
		Успех = Документы.Операция.СоздатьИЗаполнить(ПараметрыЗаполненияДокумента);
		ПараметрыЗаполненияДокумента.ВидСредств = ВидСписываемыхСредств;
		ПараметрыЗаполненияДокумента.Сумма = -Сумма;
		Успех = Документы.Операция.СоздатьИЗаполнить(ПараметрыЗаполненияДокумента);
		
		Если Успех Тогда
			ЗафиксироватьТранзакцию();
			ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОтветаУспешнаяОперация(ПараметрыОперации);
		Иначе
			ОтменитьТранзакцию();
			ПараметрыОперации.ТекстОтвета = ПараметрыОперации.ТекстОтвета + ТекстОтветаОшибка();
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры
 
Функция ТекстОтветаОшибка()
	
	Возврат "Произошла непредвиденная ошибка. Пожалуйста, обратитесь к разработчику.";
	
КонецФункции
 

#КонецОбласти 
