
#Область ПрограммныйИнтерфейс

Процедура ПриНачалеРаботыСистемы() Экспорт
	
	ТекПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	
	Пользователь = Справочники.Пользователи.НайтиПоРеквизиту("ИдентификаторПользователяИБ", ТекПользователь.УникальныйИдентификатор);
	Если Пользователь.Пустая() Тогда
		Пользователь = Справочники.Пользователи.НайтиПоНаименованию(ТекПользователь.Имя);
		Если Пользователь.Пустая() Тогда
			Пользователь = Справочники.Пользователи.СоздатьИЗаполнитьДанными(ТекПользователь.Имя, ТекПользователь.УникальныйИдентификатор);
			Сообщить("Был создан новый пользователь");
		Иначе
			Спр = Пользователь.ПолучитьОбъект();
			Спр.ИдентификаторПользователяИБ = ТекПользователь.УникальныйИдентификатор;
			Спр.Записать();
		КонецЕсли; 
	КонецЕсли;
	
	ПараметрыСеанса.ТекущийПользователь = Пользователь;
	
КонецПроцедуры
 
Функция ПолучитьПараметрыHTTPЗапроса() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяМетода", "GET");
	Результат.Вставить("ТекстЗапроса", "");
	Результат.Вставить("Заголовки", Новый Соответствие);
	Результат.Вставить("ТелоЗапроса", "");
	
	Возврат Результат;
	
КонецФункции

Функция ВыполнитьHTTPЗапрос(ПараметрыСоединения, ПараметрыЗапроса) Экспорт
	
	Соединение = ПараметрыСоединения.Соединение;
	
	Запрос = Новый HTTPЗапрос(ПараметрыЗапроса.ТекстЗапроса, ПараметрыЗапроса.Заголовки); 
	
	Если ТипЗнч(ПараметрыЗапроса.ТелоЗапроса) = Тип("ДвоичныеДанные") Тогда
		Запрос.УстановитьТелоИзДвоичныхДанных(ПараметрыЗапроса.ТелоЗапроса);
	Иначе
		Запрос.УстановитьТелоИзСтроки(ПараметрыЗапроса.ТелоЗапроса);
	КонецЕсли;
	
	Ответ = Соединение.ВызватьHTTPМетод(ПараметрыЗапроса.ИмяМетода, Запрос);
	СтрокаОтвет = Ответ.ПолучитьТелоКакСтроку();
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(СтрокаОтвет);
	Попытка
		СтруктураОтвета = ПрочитатьJSON(Чтение);
	Исключение
	    СтруктураОтвета = Неопределено;
	КонецПопытки; 
	
	Результат = Новый Структура;
	Результат.Вставить("Ответ", Ответ);
	Результат.Вставить("СтрокаОтвет", СтрокаОтвет);
	Результат.Вставить("ДвоичныеДанныеОтвет", Ответ.ПолучитьТелоКакДвоичныеДанные());
	Результат.Вставить("СтруктураОтвета", СтруктураОтвета);
	
	Возврат Результат;
	
КонецФункции

Функция СписокПрокси(Параметры) Экспорт
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Ссылка");
	Результат.Колонки.Добавить("ТипПрокси");
	Результат.Колонки.Добавить("Сервер");
	Результат.Колонки.Добавить("Порт");
	Результат.Колонки.Добавить("Пользователь");
	Результат.Колонки.Добавить("Пароль");
	
	Если Параметры.ИспользоватьПрокси Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Прокси.Ссылка КАК Ссылка,
		|	Прокси.ТипПрокси КАК ТипПрокси,
		|	Прокси.Сервер КАК Сервер,
		|	Прокси.Порт КАК Порт,
		|	Прокси.Пользователь КАК Пользователь,
		|	Прокси.Пароль КАК Пароль
		|ИЗ
		|	Справочник.Прокси КАК Прокси
		|ГДЕ
		|	НЕ Прокси.ПометкаУдаления";
		РезультатЗапроса = Запрос.Выполнить();	
		Если Не РезультатЗапроса.Пустой() Тогда	
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = Результат.Добавить();
				НоваяСтрока.Ссылка = Выборка.Ссылка;
				НоваяСтрока.ТипПрокси = Нрег(Выборка.ТипПрокси);
				НоваяСтрока.Сервер = Выборка.Сервер;
				НоваяСтрока.Порт = Выборка.Порт;
				НоваяСтрока.Пользователь = Выборка.Пользователь;
				НоваяСтрока.Пароль = Выборка.Пароль;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли; 
	
	Результат.Добавить();
	
	Возврат Результат;
	
КонецФункции

Процедура ОбновитьПрокси() Экспорт
	
	Соединение = Новый HTTPСоединение("www.proxy-list.download",443,,,,,Новый ЗащищенноеСоединениеOpenSSL());	
	Запрос = Новый HTTPЗапрос("/api/v1/get?type=socks5&anon=elite&country=US");
	Ответ = Соединение.Получить(Запрос,);
	Если Ответ.КодСостояния = 200 Тогда
		СтрокаОтвет = Ответ.ПолучитьТелоКакСтроку();
		МассивОтвета = СтрРазделить(СтрокаОтвет, Символы.ПС);
		Для Каждого Прокси Из МассивОтвета Цикл
			
			Данные = СтрРазделить(Прокси, ":");
			Если Не ЗначениеЗаполнено(Данные[0]) Тогда
				Продолжить;
			КонецЕсли; 
			
			Если Данные.Количество() = 1 Тогда
				Порт = 8080;
			Иначе
				Порт = Число(Данные[1]);
			КонецЕсли; 
			Сервер = Данные[0];
			
			НовСпр = Справочники.Прокси.СоздатьЭлемент();
			НовСпр.Наименование = Прокси;
			НовСпр.ТипПрокси = Перечисления.ТипыПрокси.socks5;
			НовСпр.Сервер = Сервер;
			НовСпр.Порт = Число(Порт);
			НовСпр.Записать();
			
		КонецЦикла; 
	КонецЕсли; 
		
КонецПроцедуры

Процедура ОчиститьНерабочиеПрокси() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Прокси.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Прокси КАК Прокси
	|ГДЕ
	|	Прокси.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Попытка
				Выборка.Ссылка.ПолучитьОбъект().Удалить();
			Исключение
			    // запись в жр
			КонецПопытки; 	
		КонецЦикла; 
	КонецЕсли; 
	 	
	
КонецПроцедуры
 
Функция МенеджерОбъектаПоПолномуИмени(ПолноеИмя) Экспорт
	Перем КлассОМ, ИмяОМ, Менеджер;
	
	ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
	
	Если ЧастиИмени.Количество() >= 2 Тогда
		КлассОМ = ЧастиИмени[0];
		ИмяОМ  = ЧастиИмени[1];
	КонецЕсли;
	
	Если      ВРег(КлассОМ) = "ПЛАНОБМЕНА" Тогда
		Менеджер = ПланыОбмена;
		
	ИначеЕсли ВРег(КлассОМ) = "СПРАВОЧНИК" Тогда
		Менеджер = Справочники;
		
	ИначеЕсли ВРег(КлассОМ) = "ДОКУМЕНТ" Тогда
		Менеджер = Документы;
		
	ИначеЕсли ВРег(КлассОМ) = "ЖУРНАЛДОКУМЕНТОВ" Тогда
		Менеджер = ЖурналыДокументов;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЕРЕЧИСЛЕНИЕ" Тогда
		Менеджер = Перечисления;
		
	ИначеЕсли ВРег(КлассОМ) = "ОТЧЕТ" Тогда
		Менеджер = Отчеты;
		
	ИначеЕсли ВРег(КлассОМ) = "ОБРАБОТКА" Тогда
		Менеджер = Обработки;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНВИДОВХАРАКТЕРИСТИК" Тогда
		Менеджер = ПланыВидовХарактеристик;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНСЧЕТОВ" Тогда
		Менеджер = ПланыСчетов;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНВИДОВРАСЧЕТА" Тогда
		Менеджер = ПланыВидовРасчета;
		
	ИначеЕсли ВРег(КлассОМ) = "РЕГИСТРСВЕДЕНИЙ" Тогда
		Менеджер = РегистрыСведений;
		
	ИначеЕсли ВРег(КлассОМ) = "РЕГИСТРНАКОПЛЕНИЯ" Тогда
		Менеджер = РегистрыНакопления;
		
	ИначеЕсли ВРег(КлассОМ) = "РЕГИСТРБУХГАЛТЕРИИ" Тогда
		Менеджер = РегистрыБухгалтерии;
		
	ИначеЕсли ВРег(КлассОМ) = "РЕГИСТРРАСЧЕТА" Тогда
		Если ЧастиИмени.Количество() = 2 Тогда
			// Регистр расчета
			Менеджер = РегистрыРасчета;
		Иначе
			КлассПодчиненногоОМ = ЧастиИмени[2];
			ИмяПодчиненногоОМ = ЧастиИмени[3];
			Если ВРег(КлассПодчиненногоОМ) = "ПЕРЕРАСЧЕТ" Тогда
				// Перерасчет
				Попытка
					Менеджер = РегистрыРасчета[ИмяОМ].Перерасчеты;
					ИмяОм = ИмяПодчиненногоОМ;
				Исключение
					Менеджер = Неопределено;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВРег(КлассОМ) = "БИЗНЕСПРОЦЕСС" Тогда
		Менеджер = БизнесПроцессы;
		
	ИначеЕсли ВРег(КлассОМ) = "ЗАДАЧА" Тогда
		Менеджер = Задачи;
		
	ИначеЕсли ВРег(КлассОМ) = "КОНСТАНТА" Тогда
		Менеджер = Константы;
		
	ИначеЕсли ВРег(КлассОМ) = "ПОСЛЕДОВАТЕЛЬНОСТЬ" Тогда
		Менеджер = Последовательности;
	КонецЕсли;
	
	Если Менеджер <> Неопределено Тогда
		Попытка
			Возврат Менеджер[ИмяОМ];
		Исключение
			Менеджер = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	ВызватьИсключение НСтр("ru = 'Неизвестный тип объекта метаданных """ + ПолноеИмя + """'");
	
КонецФункции

Процедура УстановитьЗначениеОтбора(ЭлементОтбора, Значение) Экспорт
	
	ЭлементОтбора.Использование = Истина;
	
	ТипЗначенияОтбора = ТипЗнч(Значение);
	Если ТипЗначенияОтбора = Тип("Массив") Тогда
		ЭлементОтбора.ВидСравнения = ВидСравнения.ВСписке;
		ЗначениеОтбора = Новый СписокЗначений;
		ЗначениеОтбора.ЗагрузитьЗначения(Значение);
	ИначеЕсли ТипЗначенияОтбора = Тип("СписокЗначений") Тогда
		ЭлементОтбора.ВидСравнения = ВидСравнения.ВСписке;
		ЗначениеОтбора = Значение;
	Иначе
		ЗначениеОтбора = Значение;
	КонецЕсли;
	
	ЭлементОтбора.Значение = ЗначениеОтбора;
	
КонецПроцедуры

Функция СклоненияСтроки(Знач Строка, Падеж = "") Экспорт
	
	ПараметрыСоединения = Новый Структура;
	Соединение = Новый HTTPСоединение("ws3.morpher.ru",443,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	ПараметрыСоединения.Вставить("Соединение", Соединение);
	
	ПараметрыЗапроса = ПолучитьПараметрыHTTPЗапроса();
	ПараметрыЗапроса.ТекстСообщения = "/russian/declension?s=" + СтрЗаменить(Строка, " ", "%20") + "&format=json";
	
	СтруктураОтвета = ВыполнитьHTTPЗапрос(ПараметрыСоединения, ПараметрыЗапроса).СтруктураОтвета;
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Падеж", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Значение", Новый ОписаниеТипов("Строка"));
	
	НовСтрока = Результат.Добавить();
	НовСтрока.Падеж = "И";
	НовСтрока.Значение = Строка;
		
	Для Каждого Ответ Из СтруктураОтвета Цикл
		Если Не ЗначениеЗаполнено(Падеж) Или Падеж = Ответ.Ключ Тогда
			НовСтрока = Результат.Добавить();
			НовСтрока.Падеж = Ответ.Ключ;
			НовСтрока.Значение = Ответ.Значение;
		КонецЕсли; 	 
	КонецЦикла; 
	
	Возврат Результат;
	
КонецФункции
  
Функция ДатаАпи(Дата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(&ПромежуточнаяДата, ЧАС, &ЧасовойПояс) КАК Дата";
	Запрос.УстановитьПараметр("ПромежуточнаяДата", '19700101' + Дата);
	Запрос.УстановитьПараметр("ЧасовойПояс", Константы.ЧасовойПояс.Получить());
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	РезультатЗапроса.Следующий();
	 
	Возврат РезультатЗапроса.Дата;
	
КонецФункции

Функция КоличествоЭлементовТаблицы(ИмяТаблицы) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(1) КАК Количество
	|ИЗ
	|	" + ИмяТаблицы + " КАК Таблица";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Количество;
	КонецЕсли; 	 
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьОстаткиПроксиИУведомитьАдминистратора()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Прокси.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.Прокси КАК Прокси";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.Количество <= Константы.МинимальныйОстатокПрокси.Получить() Тогда
		
		МассивПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
		
		Для Каждого Пользователь Из МассивПользователей Цикл
			ЕстьРольАдминистратора = Ложь;
			Для Каждого Роль Из Пользователь.Роли Цикл
				Если Роль.Имя = "Администратор" Тогда
					ЕстьРольАдминистратора = Истина;
					Прервать;
				КонецЕсли; 
			КонецЦикла; 
			
			Если ЕстьРольАдминистратора Тогда
				
				УИД = Пользователь.УникальныйИдентификатор;
				
				СпрПользователь = Справочники.Пользователи.НайтиПоРеквизиту("ИдентификаторПользователяИБ",УИД);
				
				Если Не СпрПользователь.Пустая() И ЗначениеЗаполнено(СпрПользователь.ИДЧата) Тогда
					
					ИДБота = Константы.ИДБота.Получить();
					Соединение = ОбменТелеграм.ПодключениеИПроверкаТелеграм(ИДБота);
					Если Соединение = Неопределено Тогда
						Возврат;
					КонецЕсли;
					
					Сообщение = "В базе бюджета заканчиваются прокси. Осталось " + Выборка.Количество + " элементов. Заполните список прокси новыми данными.";
					
					ОбменТелеграм.ОтправитьСообщение(Соединение, ИДБота, СпрПользователь.ИДЧата, Сообщение);
					
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



 