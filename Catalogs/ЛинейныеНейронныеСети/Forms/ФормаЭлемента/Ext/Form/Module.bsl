
&НаСервере
Процедура ЗагрузитьДанныеОбученияНаСервере(Адрес)
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	СправочникОбъект.ЗагрузитьДанныеОбученияИзФайла(Адрес);
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	ОбновитьОтображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеОбучения(Команда)
	
	Если ХранилищеЗаполнено() Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Данные обучения уже загружены. Заменить?",РежимДиалогаВопрос.ДаНет, 60); 
	Иначе	
		НачатьЗаполнениеДанныхОбучения();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		НачатьЗаполнениеДанныхОбучения();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаполнениеДанныхОбучения()
	
	Если Объект.Предопределенный Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросТипЗаполненияЗавершения", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Данный элемент имеет встроенный файл обучения. Заполнить данные из него?",РежимДиалогаВопрос.ДаНетОтмена, 60); 
	Иначе	
		ЗаполнитьДанныеОбученияИзФайла();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросТипЗаполненияЗавершения(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДанныеОбученияИзОбщегоМакета();
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросАвтоматическоеОбучение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Данные загружены. Переобучить нейронную сеть?",РежимДиалогаВопрос.ДаНет, 60);
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда 	
		ЗаполнитьДанныеОбученияИзФайла();
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросАвтоматическоеОбучение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Данные загружены. Переобучить нейронную сеть?",РежимДиалогаВопрос.ДаНет, 60);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеОбученияИзФайла()
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Показать(Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОбученияИзОбщегоМакета()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	СправочникОбъект.ЗаполнитьДанныеОбученияИзОбщегоМакета();
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	ОбновитьОтображение();
		
КонецПроцедуры
 
&НаКлиенте
Процедура ВыборФайлаЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		ДД = Новый ДвоичныеДанные(ВыбранноеЗначение[0]);
		
		Адрес = ПоместитьВоВременноеХранилище(ДД);
		ЗагрузитьДанныеОбученияНаСервере(Адрес);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросАвтоматическоеОбучение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "Данные загружены. Переобучить нейронную сеть?",РежимДиалогаВопрос.ДаНет, 60); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросАвтоматическоеОбучение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ДобавитьУниграммыИзФайлаОбученияНейроннойСети(Объект.Ссылка);
		НейронныеСетиКлиент.НачатьОбучениеНейроннойСети(ЭтотОбъект, Объект.Ссылка);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьУниграммыИзФайлаОбученияНейроннойСети(НейроннаяСеть)

	Справочники.Униграммы.ДобавитьУниграммыИзФайлаОбученияНейроннойСети(НейроннаяСеть);
	
КонецПроцедуры

&НаСервере
Функция ХранилищеЗаполнено()
	
	Возврат РеквизитФормыВЗначение("Объект").ДанныеОбучения.Получить() <> Неопределено;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// временный костыль
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ВидВходаНейрона = ПланыВидовХарактеристик.ВидыВходовНейрона.Униграммы;
	КонецЕсли; 
	
	ОбновитьОтображение();
	 	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображение()
	
	Если Не ХранилищеЗаполнено() Тогда
		Элементы.СтатусЗаполненияДанныхОбучения.Заголовок = "Данные обучения не заполнены!";
	КонецЕсли;
	
КонецПроцедуры
 
&НаКлиенте
Процедура СтатусЗаполненияДанныхОбученияНажатие(Элемент)
	
	ЗагрузитьДанныеОбучения(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанныеОбучения(Команда)
	
	Если Не ХранилищеЗаполнено() Тогда
		Сообщить("Нет данных для заполнения");
	КонецЕсли; 
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = "Текстовый документ (*.txt)|*.txt";
	Диалог.Показать(Новый ОписаниеОповещения("СохранениеФайлаЗавершение", ЭтотОбъект)); 
	
КонецПроцедуры

&НаКлиенте
Процедура СохранениеФайлаЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		Адрес = ПолучитьДД();
		ДД = ПолучитьИзВременногоХранилища(Адрес);
		ДД.Записать(ВыбранноеЗначение[0]);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДД()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	ДД = СправочникОбъект.ДанныеОбучения.Получить();
	Возврат ПоместитьВоВременноеХранилище(ДД);
	
КонецФункции
 
 